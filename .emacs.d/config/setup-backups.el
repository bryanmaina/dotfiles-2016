;; Disabling backups for su and sudo methods
(setq backup-enable-predicate
      (lambda (name)
        (and (normal-backup-enable-predicate name)
             (not
              (let ((method (file-remote-p name 'method)))
                (when (stringp method)
                  (member method '("su" "sudo"))))))))

;; Backing up intermediate copies.
(setq vc-make-backup-files t)

;; handle multiple backups
(setq backup-by-copying t      ; Don't clobber symlinks.
      delete-old-versions t    ; Don't ask to delete excess backup versions.
      kept-new-versions 10     ; Number of newest versions to keep.
      kept-old-versions 0      ; Number of oldest versions to keep.
      version-control t)       ; Use version numbers for backups.

;; Default and per-save backups go here:
(setq backup-directory-alist '(("" . "~/.emacs.d/backup/per-save")))

(defun force-backup-of-buffer ()
  ;; Make a special "per session" backup at the first save of each
  ;; emacs session.
  (when (not buffer-backed-up)
    ;; Override the default parameters for per-session backups.
    (let ((backup-directory-alist '(("" . "~/.emacs.d/backup/per-session")))
	  (kept-new-versions 3))
      (backup-buffer)))
  ;; Make a "per save" backup on each save.  The first save results in
  ;; both a per-session and a per-save backup, to keep the numbering
  ;; of per-save backups consistent.
  (let ((buffer-backed-up nil))
    (backup-buffer)))

(add-hook 'before-save-hook  'force-backup-of-buffer)
